import xy.ctti;
import libxy.(stdio, string, memory);

;; MyStruct comment
struct MyStruct {
    ;; My name
    name: Str;

    ;; Some number
    num: Ulong;

    integer: Int = -1;

    ;; Array of
    ;; floats
    arr: @Float[10];

    next: Ptr~MyStruct;
}

struct FieldDesc{
    name: Str;
    typename: Str;
    size: Usize;
    offset: Usize;
    alignment: Usize;
    comment: Str;
}

func printTypeInfo(
    s: pseudo Any,
    typeName := nameof(%s),
    typeComment := commentof(%s),
    fields: pseudo @FieldDesc[] = @for (f in s'fieldsof) FieldDesc{
        name=f'nameof,
        typename=nameof(%f),
        size=f'sizeof,
        offset=f'offsetof,
        alignment=f'alignof,
        comment=f'commentof,
    },
    fieldsPtr : Ptr~FieldDesc = fields'addrof,
    fieldsLen : Usize = fields'len
) {
    if (typeComment'isEmpty'not) {
        print(f";; {typeComment}\n");
    }
    print(f"struct {typeName} \{\n");
    for (i in :fieldsLen) {
        f := fieldsPtr[i];
        if (f.comment'isEmpty'not) {
            print(f"    ;; {f.comment}\n");
        }
        print(f"    {f.name}: {f.typename}; # size={f.size} offset={f.offset} alignof={f.alignment}\n");
    }
    print("}\n");
}

func main~EntryPoint() {
    s := MyStruct{};
    printTypeInfo(s);
}